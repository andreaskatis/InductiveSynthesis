\section{Case Studies}

\subsection{A simple controller example}

In this section, we provide an illustrative example of how the synthesis
algorithm creates a simple implementation from specifications describing the
constraints that a controller must meet. The specification is written in the
Lustre language, and can be seen in Figure~\ref{fig:example}. The controller
is used to maintain an appropriate level of \textit{speed} at each next step of
its execution, using two auxillary signals, called \textit{plus} and
\textit{minus} to help determine future decisions on acceleration or deceleration. There is
only one input to this example, called \textit{diff}, and is used to compute
the amount by which the \textit{speed} value changes with each new state.

The specification is composed of an auxillary node called Sofar, that is a
custom operation on a boolean variable to capture whether it has been
historically true up to and including the current step. The rest of the nodes
defined essentially cover the assumptions and the guarantees that the contract
contains. The node \textit{Environment} is used to describe restrictions on the
input variable \textit{diff}, while the system's correct response is captured by the
node \textit{Property}. The \textit{top} node of the specification is used as
the main block of this program, and combines the two constraints to effectively
define the structure of the final property that the system should be respecting
at all states during its execution.

\andreas{add discription of Controller node. Add text for synthesis case}

 \begin{figure}[H]
 \begin{lstlisting}[basicstyle=\ttfamily\footnotesize]
--
-- Source Bertrand Jeannet, NBAC tutorial
--

node Sofar( X : bool ) returns ( Sofar : bool );
let
    Sofar = X -> X and pre Sofar;
tel

node Environment(diff: int; plus,minus: bool) returns (ok: bool);
let
  ok = (-4 <= diff and diff <= 4) and 
     (if (true -> pre plus) then diff >= 1 else true) and
     (if (false -> pre minus) then diff <= -1 else true);
tel

node Controller(diff: int) returns (speed: int; plus,minus: bool);
let
  speed = 0 -> pre(speed)+diff;
  plus = speed <= 9;
  minus = speed >= 11;
tel

node Property(speed: int) returns (ok: bool);
var cpt: int;
    acceptable: bool;
let
  acceptable = 8 <= speed and speed <= 12;
  cpt = 0 -> if acceptable then 0 else pre(cpt)+1;
  ok = true -> (pre cpt<=7);
tel

--@ ensures OK;
node top(diff:int) returns (OK: bool);
var speed: int; 
    plus,minus,realistic: bool;
let
  (speed,plus,minus) = Controller(diff);
  realistic =  Environment(diff,plus,minus);

  OK = Sofar( realistic and 0 <= speed and speed < 16 ) => Property(speed);
  --%PROPERTY OK;
  --%MAIN;
tel 
\end{lstlisting}
 \caption{Specification for a Controller in Lustre}
 \label{fig:example}
 \end{figure}
 

\label{sec:cstudies}

